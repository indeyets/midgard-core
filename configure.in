dnl $Id$
dnl
dnl Process this file with autoconf to produce a configure script.
dnl
dnl The Midgard version number is changed from here. First edit
dnl this file, then write a ChangeLog entry about the new version,
dnl and finally commit the source tree and give it a tag.
dnl
dnl If the version contains user visible changes then you should
dnl have updated the NEWS file. Remember also to test the release
dnl before committing.
dnl
AC_INIT([midgard3-core],[10.05.99])
AC_PREREQ(2.50)
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_SRCDIR(src/midgard.c)

AC_SUBST(MIDGARD_ABI_MAJOR, 3)
AC_SUBST(MIDGARD_ABI_MINOR, 0)
AC_SUBST(MIDGARD_RELEASE_MAJOR, 2010)
AC_SUBST(MIDGARD_RELEASE_MINOR, 11)

dnl Automake is needed for the build system.
dnl
AM_INIT_AUTOMAKE([-Wno-portability])
AM_MAINTAINER_MODE
GOBJECT_INTROSPECTION_CHECK([0.6.5])

AM_PROG_LEX
AC_PROG_YACC
AC_PROG_CC(gcc cc)
AM_PROG_CC_C_O
AC_LANG(C)

dnl Checks for programs.
AM_PROG_LIBTOOL

dnl Checks for libraries.

_MIDGARD_DBUS_SUPPORT=yes
dbus_libs="dbus-1 dbus-glib-1"
MIDGARD_DBUS_SUPPORT=1
AC_MSG_CHECKING([whether to compile dbus support (enabled by default)])
AC_ARG_WITH(dbus-support,
	[  --with-dbus-support    compile with dbus support ],
	_MIDGARD_DBUS_SUPPORT=[$]withval, _MIDGARD_DBUS_SUPPORT="yes")
AC_MSG_RESULT($_MIDGARD_DBUS_SUPPORT)
if test "$_MIDGARD_DBUS_SUPPORT" == "no"; then
	#MIDGARD_DBUS_SUPPORT=0
	dbus_libs=""
fi

PKG_CHECK_MODULES(MIDGARD, glib-2.0 gobject-2.0 libxml-2.0 libgda-4.0 $dbus_libs)

DBUS_CONF_DIR="/etc"
if test "$_MIDGARD_DBUS_SUPPORT" == "yes"; then
	#MIDGARD_DBUS_SUPPORT=1
	MIDGARD_CFLAGS=" ${MIDGARD_CFLAGS} -DMGD_HAVE_DBUS "
	DBUS_CONF_DIR=`pkg-config --variable sysconfdir dbus-1`
fi
AM_CONDITIONAL(MIDGARD_DBUS_SUPPORT, test x$_MIDGARD_DBUS_SUPPORT = xyes)

LIBS="$INTLLIBS $LIBS"

PATH="${PATH}:${prefix}/bin"

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.

dnl accomodate Solaris
AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(htonl, socket)

AC_MSG_CHECKING([if building for some Win32 platform])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test x$platform_win32 = xyes)

if test "$prefix" = "/usr"; then
	sysconfdir="/etc/midgard3"
elif test "$prefix" = "NONE"; then
	sysconfdir="/usr/local/etc/midgard3"
else 
	sysconfdir="$prefix/etc/midgard3"
fi

LIBS="$LIBS $MIDGARD_LIBS"

AC_SUBST(LIBS)
AC_SUBST(VERSION)
AC_SUBST(VERSION_STRING)
AC_SUBST(LFLAGS)
AC_SUBST(MIDGARD_DBUS_SUPPORT)
AC_SUBST(LIBGDA_VERSION, $gdamodule)
AC_SUBST(MIDGARD_LIBS)
AC_SUBST(MIDGARD_CFLAGS)
AC_SUBST(DBUS_CONF_DIR)

AC_CONFIG_FILES([Makefile src/midgard_config_auto.h midgard3.pc])
AC_OUTPUT
